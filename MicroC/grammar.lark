
// Gramática inicial para MicroC (exemplo simplificado)

?start:         program

program:        (declaration)*

?declaration:   var_decl 
                | fun_decl

var_decl:       type ID ";"
fun_decl:       type ID "(" params ")" block

params:         (param ("," param)*)?
param:          type ID

type:           "int" | "bool" | "void"

block:          "{" statement* "}"
?statement:      expr_stmt 
                | block
                | if_stmt 
                | while_stmt 
                | return_stmt
                | var_decl

expr_stmt:      expression ";"
if_stmt:        "if" "(" expression ")" statement ("else" statement)?
while_stmt:     "while" "(" expression ")" statement
return_stmt:    "return" expression? ";"

?expression:     assignment
?assignment:     ID "=" assignment | logic_or
?logic_or:       logic_and ("||" logic_and)*
?logic_and:      equality ("&&" equality)*
?equality:       relational (("==" | "!=" ) relational)*
?relational:     sum (("<" | ">" | "<=" | ">=") sum)*
?sum: term (PLUS term | MINUS term)*
?term:           factor (("*" | "/") factor)*
?factor:          INT 
                 | BOOL
                 | "!" factor
                 | ID 
                 | ID "(" [args] ")"      // <- chamada de função
                 | "(" expression ")"

args: expression ("," expression)*
NOT: "!"
ID: /[a-zA-Z_][a-zA-Z0-9_]*/
BOOL: "true" | "false"
INT: /[0-9]+/

PLUS: "+"
MINUS: "-"
TIMES: "*"
DIVIDE: "/"
EQ: "=="
NE: "!="
LT: "<"
GT: ">"
LE: "<="
GE: ">="
OR: "||"
AND: "&&"

%import common.WS
%ignore WS
